
PREFIX :		<http://agentlab.ru/2019/edu_ontology#>
PREFIX owl:		<http://www.w3.org/2002/07/owl#>
PREFIX rdf:		<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml:		<http://www.w3.org/XML/1998/namespace>
PREFIX xsd:		<http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccso:	<https://w3id.org/ccso/ccso#>
PREFIX skosxl:	<http://www.w3.org/2008/05/skos-xl#>
PREFIX esco:	<http://data.europa.eu/esco/model#>

SELECT ?syllabus_ ?code ?competence ?level ?descriptor
WHERE {
	# Получение кода суос и его компетенции
	?suos_competence_ 	a esco:Skill ;
						skosxl:prefLabel ?label_ .
	?label_ skosxl:literalForm ?competence .
	?suos_ 	:hasSkill ?suos_competence_ ;
			:hasCode ?code .
	
	# Получения описания компетенции
	?descr_	esco:isOptionalSkillFor ?suos_competence_ ;
				esco:skillType ?type ;
				skosxl:prefLabel ?descr_label_ .
	?descr_label_ skosxl:literalForm ?descriptor .
	?syllabus_ ccso:aimsToLO ?suos_competence_.
	
	# Задание освоения
    VALUES (?level ?type) {		# В теории можно задать это в модели
            ( "знать" <http://data.europa.eu/esco/skill-type/knowledge> )
            ( "уметь" <http://data.europa.eu/esco/skill-type/skill> )
            ( "владеть" :application)
	}
}
ORDER BY ?code
