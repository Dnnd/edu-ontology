
PREFIX :		<http://agentlab.ru/2019/edu_ontology#>
PREFIX owl:		<http://www.w3.org/2002/07/owl#>
PREFIX rdf:		<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml:		<http://www.w3.org/XML/1998/namespace>
PREFIX xsd:		<http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs:	<http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccso:	<https://w3id.org/ccso/ccso#>
PREFIX skosxl:	<http://www.w3.org/2008/05/skos-xl#>
PREFIX esco:	<http://data.europa.eu/esco/model#>
PREFIX dm:		<http://data.15926.org/dm/>


SELECT ?teacher ?event ?start ?end
WHERE {
	?teacher a ccso:Lecturer .

	?shadow_conn a ?t .
	?t rdfs:subClassOf* dm:TemporalWholePart .
	?shadow_conn dm:hasWhole ?teacher .
	?shadow_conn dm:hasPart ?shadow .

	?process_conn a ?p .
	?p rdfs:subClassOf* dm:Participation .
	?process_conn dm:hasPart ?shadow .
	?process_conn dm:hasWhole ?process .

	?process :representsTE ?event .

	?start a ?s .
	?s rdfs:subClassOf* dm:RepresentationOfGregorianDateAndUtcTime .
	?pit_conn dm:hasPattern ?start .
	?pit_conn dm:hasRepresented ?pit .

	?beg_conn a ?b .
	?b rdfs:subClassOf* dm:Beginning .
	?beg_conn dm:hasPart ?pit .
	?beg_conn dm:hasWhole ?process .

	?end a ?e .
	?e rdfs:subClassOf* dm:RepresentationOfGregorianDateAndUtcTime .
	?pit_end_conn dm:hasPattern ?end .
	?pit_end_conn dm:hasRepresented ?pit_end .

	?end_conn a ?f .
	?f rdfs:subClassOf* dm:Ending .
	?end_conn dm:hasPart ?pit_end .
	?end_conn dm:hasWhole ?process .


}

